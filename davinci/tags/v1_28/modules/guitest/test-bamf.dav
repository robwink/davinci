#

verbose = 0

load_module("gui")

define find_band(obj) {
        bands = obj.qube.band_bin.band_bin_original_band
        for (i = 1 ; i <= length(bands) ; i+=1) {
                if (bands[i] == $1) return(i);
        }
        return(1)
}

define sstretch(variance) {
        b = avg($1, ignore=-32768, both=1)
        v = 40
        if (HasValue(variance)) {
                v = variance
        }
        c = ($1 - float(b.avg))/float(b.stddev)*v+127
        return(byte(c))
}

define showVicar(widgetMap) {

  fileName = $1
  data = load_pds(fileName)
  band = find_band(obj = data, 3)
  data = data.qube.data[,,band]
#  data[where data < 0] = 0

  vtop = gui.create("topLevelShell", name=fileName)
  vrc = gui.create("rowcolumn", vtop, { orientation = "HORIZONTAL" })
  variance = 50
  scale = gui.create("scale", vrc, { value = variance })
  vicar = gui.create("vicar", vrc, { image = sstretch(data, variance = variance) })
  gui.realize(vtop)

  newVicar = { vicarWidgetId = vicar, scaleWidgetId = scale, data = data }
  add_struct(widgetMap, name = sprintf("%d", scale), value = newVicar)
  add_struct(widgetMap, name = sprintf("%d", vicar), value = newVicar)

}

define restretch() {

  vicarInfo = $1
  vicarId = vicarInfo.vicarWidgetId
  stretchValue = gui.get(vicarInfo.scaleWidgetId).value
  gui.set(vicarId, { image = sstretch(vicarInfo.data, variance = stretchValue + 1) })

}

top = gui.create("topLevelShell", name="Davinci")
fs = gui.create("fileSelectionBox", top, { pattern = "*.qub" })

gui.realize(top)

map = struct()

ok = 1
while (ok == 1) {
  event = gui.wait()
  if (event.widget == fs) {
    if (event.event == "cancel") {
      ok = 0
    } else if (event.event == "ok") {
      showVicar(widgetMap = map, gui.get(event.widget).dirSpec)
    } else if (event.event == "help") {
        printf("\n\nSelect a file in the FileSelectionBox and click OK to spawn a VICAR widget.\n\n")
    }
  } else {

    # See if it was a scale change..

    if (event.event == "valueChanged") {
      for (i = 1; i <= length(map); i = i + 1) {
        vicarInfo = map[i];
        if (event.widget == vicarInfo.scaleWidgetId) {
            restretch(vicarInfo)
          }
        }
      }
    }
  }
}

gui.destroy(top)
